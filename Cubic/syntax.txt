Program ::= Block
Block ::= {Statement} [ReturnStmt]

///////////////////////////////////////////////////////////////////////////////
// Statements
///////////////////////////////////////////////////////////////////////////////

Statement ::= ';'    
    | DeclarationStmt
    | FunctionDef
    | ClassDef
    | BlockStmt
    | IfStmt 
    | ForStmt
    | WhileStmt
    | RepeatStmt
    | PassStmt
    | MatchStmt
    | YieldStmt
    | AssertStmt
    | ExpressionStmt

DeclarationStmt ::= ValDecl | RefDecl
BlockStmt ::= 'do' Block 'end'
IfStmt ::= 'if' Expr 'then' Block {'elif' Expr 'then' Block} ['else' Block] 'end'
ForStmt ::= 'for' Id 'in' Expr ['step' Expr] 'do' Block 'end'
WhileStmt ::= 'while' Expr 'do' Block 'end'
RepeatStmt ::= 'repeat' Block 'until' Expr
YieldStmt ::= 'yield' Expr
PassStmt ::= 'pass'
ReturnStmt ::= 'return' [Expr {',' Expr}]
AssertStmt ::= 'assert' Expr
ExpressionStmt ::= AssignmentExpr | PrefixExpr

// TODO: Consider adding syntax for having nested `MatchStatement`s.
// TODO: Add more matching patterns for lists, maps, etc.
MatchStmt ::= 'match' Expr {MatchCase} 'end'
MatchCase ::= 'case' ('_' | Type | Expr) '=>' Block

// Calling a function
FunctionCall ::= PrefixExpr FunctionCallArgs
FunctionCallArgs ::= '(' FunctionCallArg* ')'
FunctionCallArg ::= Expr

// Defining a function
FunctionDef ::= 'function' [Id '.'] [Id] FunctionBody 'end'
FunctionParam ::= [('in' | 'out')] (Id | UnionType | Id ':' UnionType)
FunctionParamList ::= FunctionParam {',' FunctionParam}
FunctionBody ::= '(' [FunctionParamList] [',' '...'] ')' ['->' UnionType] Block

///////////////////////////////////////////////////////////////////////////////
// Declarations
///////////////////////////////////////////////////////////////////////////////

VarDecl ::= IdList ':' (UnionType ['=' ExprList] | '=' ExprList)
ValDecl ::= 'val' VarDecl
RefDecl ::= 'var' VarDecl
ClassDef ::= 'class' Id [ '[' Id ':'  ']' ] {ClassMemberDecl} 'end'
ClassMemberDecl ::= [pub] [('val' | 'var')] IdList ':' UnionType ['=' ExprList]

///////////////////////////////////////////////////////////////////////////////
// Helpers
///////////////////////////////////////////////////////////////////////////////
IdList ::= Id {',' Id}
IdType ::= IdList ':' [UnionType]

VarList ::= Var {',' Var}
ExprList ::= Expr {',' Expr}

///////////////////////////////////////////////////////////////////////////////
// Typing Information
///////////////////////////////////////////////////////////////////////////////
AddressableType ::= Id | PrimitiveType | CompositeType
Type ::= ['ref'] (AddressableType ['*'] | FunctionType | 'nil') [ArrayFieldList]
UnionType ::= Type {'|' Type}

ArrayFieldDecl ::= '[' [(Number | '..')] ']'
ArrayFieldList ::= {ArrayFieldDecl}

PrimitiveType ::= ['u']'int'(8|16|32|64)
    | 'float'
    | 'double'
    | 'bool'
    | 'string'

CompositeType ::= 'Set' '[' [UnionType] ']'
    | 'Map' '[' [PrimitiveType] ':' [UnionType] ']'
    | 'List' '[' [UnionType] ']'

// Declaring a function, e.g.
//   val sum : function (int, int) -> int
FunctionType ::= '(' [UnionType {',' UnionType } [',' '...']] ')' '->' UnionType

// TODO: Extend with [1..3] syntax.
Field ::= Id '=' Expr
FieldList ::= Field {',' Field} [',']

ArrayValue ::= '[' ExprList ']'
MapValue ::= '{' [FieldList] '}'
SetValue ::= '{' [ExprList] '}'

// TODO: Make the Id optional and add type inference for this case.
ClassConstructor ::= Id '{' [ClassConstructorParamList] [',' ClassConstructorNamedParamList] '}'
ClassConstructorParam ::= Expr
ClassConstructorNamedParam ::= Id '=' Expr
ClassConstructorParamList ::= ClassConstructorParam {',' ClassConstructorParam} [',']
ClassConstructorNamedParamList ::= ClassConstructorNamedParam {',' ClassConstructorNamedParam} [',']

///////////////////////////////////////////////////////////////////////////////
// Expressions
///////////////////////////////////////////////////////////////////////////////

// TODO: This will incorrectly match `FunctionDef`s in `MatchStatement`s
Var ::= Id | PrefixExpr '[' Expr ']' | PrefixExpr '.' Id
PrefixExpr ::= '(' Expr ')' | Var | FunctionCall
Expr ::= Primary
    | ArrayValue
    | MapValue
    | SetValue
    | '...'
    | FunctionDef
    | PrefixExpr
    | Expr BinaryOp Expr
    | Expr .. Expr
    | UnaryOp Var
    | 'await' Expr
    | 'async' Expr
    | AssignmentExpr
    | ClassConstructor

AssignmentExpr ::= VarList '=' ExprList
RangeExpr ::= 

Primary ::= 'nil'
    | 'false'
    | 'true'
    | Number
    | String

BinaryOp ::= ArithmeticBinaryOp
    | BitwiseBinaryOp
    | LogicalBinaryOp
    | EqualityOp
    | RangeOp

UnaryOp ::= ArithmeticUnaryOp
    | BitwiseUnaryOp
    | LogicalUnaryOp
    | PointerOp

ArithmeticBinaryOp ::= '+' | '-' | '*' | '/' | '//' | '%'
ArithmeticUnaryOp ::= '-'

BitwiseBinaryOp ::= '&' | '|' | '^' | '<<' | '>>' 
BitwiseUnaryOp ::= '~'

LogicalBinaryOp ::= 'and' | 'or' | 'xor' | '<' | '<=' | '>' | '>='
LogicalUnaryOp ::= 'not'

EqualityOp ::= '==' | '!='
RangeOp ::= '..'
PointerOp ::= '*' | '&'